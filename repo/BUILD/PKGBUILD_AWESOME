# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sébastien Luttringer
# Contributor: xduugu
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Vesa Kaihlavirta
# Contributor: Tom Meyers <tom@odex.be>

pkgname=awesome-tos
_name=awesome-git
pkgver=4.3.530.g76f8c117
pkgrel=3
pkgdesc='Highly configurable framework window manager with the default tos skin'
url='https://awesomewm.org/'
arch=('x86_64')
license=('GPL2')
depends=('cairo' 'dbus' 'gdk-pixbuf2' 'imlib2' 'libxdg-basedir' 'lua' 'lua-lgi' 'pango'
         'startup-notification' 'xcb-util-cursor' 'xcb-util-keysyms' 'xcb-util-wm'
         'xorg-xmessage' 'libxkbcommon-x11' 'libxkbcommon' 'xcb-util-xrm' 'tos-tools' 'pacman-contrib' 'xcb-util-errors' 'playerctl' 'mantablockscreen-tos' 'awesome-freedesktop-git' 'lxsession-gtk3' 'psi-notify')
makedepends=('asciidoc' 'cmake' 'docbook-xsl' 'doxygen' 'imagemagick' 'ldoc' 'xmlto')
optdepends=('rlwrap: readline support for awesome-client'
            'dex: autostart your desktop files'
            'vicious: widgets for the Awesome window manager'
            'librsvg: for displaying SVG files without scaling artifacts')
provides=('notification-daemon')
conflicts=('awesome' 'awesome-git')
backup=('etc/xdg/awesome/rc.lua')
source=("$_name::git+https://github.com/awesomeWM/awesome.git" git://github.com/ODEX-TOS/dotfiles.git)
sha512sums=('SKIP'
            'SKIP') 
validpgpkeys=('SKIP')

pkgver() {
    cd "$_name"
    git describe | sed 's/^v//;s/-/./g'
}

prepare() {
  mkdir -p "$srcdir/$_name/"build
}

build() {
  cd "$srcdir/$_name/"build
  cmake .. \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DLUA_LIBRARY=/usr/lib/liblua.so
  make
}

package() {
  cd "$srcdir/$_name/"build
  make -C .  DESTDIR="${pkgdir}" install
  install -Dm 644 "$srcdir/$_name/"awesome.desktop -t "${pkgdir}/usr/share/xsessions"
  
  cp -r "$srcdir"/dotfiles/awesome "${pkgdir}/etc/xdg/"
  chmod 666 "${pkgdir}/etc/xdg/awesome/configuration/compton.conf"
  mkdir -p "$pkgdir"/usr/share/backgrounds/
  git clone https://github.com/ODEX-TOS/Pictures "$pkgdir"/usr/share/backgrounds/tos
  # remove the git directory from the final build
  rm -rf "$pkgdir"/usr/share/backgrounds/tos/.git
}
