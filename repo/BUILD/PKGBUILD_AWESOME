# Maintainer: Tom Meyers < tom [at] odex [dot] be>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sébastien Luttringer
# Contributor: xduugu
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Vesa Kaihlavirta

pkgname=tde
_name=awesome-git
_TVER=41
pkgver=4.3.10243.22
pkgrel=1
_LUA_VER="5.3"

pkgdesc='TDE (TOS Desktop Environment)'
url='https://tos.odex.be/'
arch=('x86_64')
license=('GPL2')
depends=('cairo' 'dbus' 'gdk-pixbuf2' 'imlib2' 'libxdg-basedir' 'lua53' 'lua53-lgi' 'pango'
         'startup-notification' 'xcb-util-cursor' 'xcb-util-keysyms' 'xcb-util-wm'
         'xorg-xmessage' 'libxkbcommon-x11' 'libxkbcommon' 'xcb-util-xrm' 'tos-tools' 'pacman-contrib' 'xcb-util-errors' 'light-locker' 'awesome-freedesktop-git' 'lxsession-gtk3' 'rofi-tos' 'python-gobject' 'python-dbus' 'picom-tryone-tos' 'upower' 'rlwrap' 'imagemagick' 'libqalculate' 'autorandr' 'udiskie' 'librsvg' 'tde-touchegg' 'qrencode')
makedepends=('asciidoc' 'cmake' 'docbook-xsl' 'doxygen' 'imagemagick' 'ldoc' 'xmlto')
optdepends=('dex: autostart your desktop files'
            'kdeconnect: Link your phone with TDE')
provides=('notification-daemon' 'awesome-tos')
conflicts=('awesome' 'awesome-git' 'awesome-tos')
backup=('etc/xdg/tde/rc.lua')

source=("$_name::git+https://github.com/ODEX-TOS/awesome.git" git://github.com/ODEX-TOS/tos-desktop-environment.git)
sha512sums=('SKIP' 'SKIP') 
validpgpkeys=('SKIP')

pkgver() {
    cd "$_name"
    printf "$(git tag | tail -n1 | sed 's/^v//;s/-/./g').$(git rev-list --count HEAD).$_TVER"
}

prepare() {
  mkdir -p "$srcdir/$_name/"build

  cd "$srcdir/$_name"
  sed -i "s/COMMAND lua\b/COMMAND lua${_LUA_VER}/" awesomeConfig.cmake tests/examples/CMakeLists.txt
  sed -i "s/LUA_COV_RUNNER lua\b/LUA_COV_RUNNER lua${_LUA_VER}/" tests/examples/CMakeLists.txt

  # TDE Lua settings
  cd "$srcdir/tos-desktop-environment"
  echo "return '$(git rev-parse HEAD)'" > "$srcdir/tos-desktop-environment/"release.lua

  cp -r "$srcdir/tos-desktop-environment/tde/" "$srcdir/$_name/build/tde-src"
}

build() {
  cd "$srcdir/$_name/"build
  cmake .. \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DLUA_INCLUDE_DIR=/usr/include/lua${_LUA_VER} \
    -DLUA_LIBRARY=/usr/lib/liblua.so.${_LUA_VER}
  make
}

package() {
  cd "$srcdir/$_name/"build
  make -C .  DESTDIR="${pkgdir}" install

  install -Dm 644 "$srcdir/$_name/tde.desktop" -t "${pkgdir}/usr/share/xsessions/"
  install -Dm 644 "$srcdir/tos-desktop-environment/tde-settings.desktop" -t "${pkgdir}/usr/share/applications/"
  
  cp -r "$srcdir"/tos-desktop-environment/tde/* "${pkgdir}/etc/xdg/tde"
  mkdir -p "${pkgdir}/etc/xdg/awesome/theme"
  cp -r "${pkgdir}/etc/xdg/tde/theme" "${pkgdir}/etc/xdg/awesome/"

  # setup images
  mkdir -p "$pkgdir"/usr/share/backgrounds/
  git clone https://github.com/ODEX-TOS/Pictures "$pkgdir"/usr/share/backgrounds/tos
  # remove the git directory from the final build
  rm -rf "$pkgdir"/usr/share/backgrounds/tos/{.git,.gitignore,.github}
  # remove the readme, only show the png/jpg files
  rm "$pkgdir"/usr/share/backgrounds/tos/README.md
  chmod -R 755 "$pkgdir"/usr/share/backgrounds/tos
}
