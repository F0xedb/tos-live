# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Sébastien Luttringer
# Contributor: xduugu
# Contributor: Ronald van Haren <ronald.archlinux.org>
# Contributor: Vesa Kaihlavirta
# Contributor: Tom Meyers <tom@odex.be>

pkgname=awesome-tos
_name=awesome-git
_TVER=8
pkgver=4.3.856.ge7113d719
pkgrel=20
pkgdesc='TDE (TOS Desktop Environment)'
url='https://tos.odex.be/'
arch=('x86_64')
license=('GPL2')
depends=('cairo' 'dbus' 'gdk-pixbuf2' 'imlib2' 'libxdg-basedir' 'lua53' 'lua53-lgi' 'pango'
         'startup-notification' 'xcb-util-cursor' 'xcb-util-keysyms' 'xcb-util-wm'
         'xorg-xmessage' 'libxkbcommon-x11' 'libxkbcommon' 'xcb-util-xrm' 'tos-tools' 'pacman-contrib' 'xcb-util-errors' 'playerctl' 'mantablockscreen-tos' 'awesome-freedesktop-git' 'lxsession-gtk3' 'psi-notify' 'rofi-tos' 'python-gobject' 'python-dbus' 'maim' 'picom-tryone-tos' 'pkgstats' 'gvfs' 'arch-audit' 'greenclip')
makedepends=('asciidoc' 'cmake' 'docbook-xsl' 'doxygen' 'imagemagick' 'ldoc' 'xmlto')
optdepends=('rlwrap: readline support for awesome-client'
            'dex: autostart your desktop files'
            'vicious: widgets for the Awesome window manager'
            'librsvg: for displaying SVG files without scaling artifacts')
provides=('notification-daemon')
conflicts=('awesome' 'awesome-git')
backup=('etc/xdg/awesome/rc.lua')
source=("$_name::git+https://github.com/awesomeWM/awesome.git" git://github.com/ODEX-TOS/dotfiles.git main_loop.patch menubar.patch config.ld ldoc.css logo.png)
sha512sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP') 
validpgpkeys=('SKIP')

pkgver() {
    cd "$_name"
    printf "$(git tag | tail -n1 | sed 's/^v//;s/-/./g').$(git rev-list --count HEAD).$_TVER"
}

prepare() {
  mkdir -p "$srcdir/$_name/"build

  cd "$srcdir/$_name"
  patch -Np1 -i "$srcdir/"main_loop.patch
  patch -Np1 -i "$srcdir/"menubar.patch
  cp "$srcdir"/config.ld docs/
  cp "$srcdir"/logo.png docs/images
  cp "$srcdir"/ldoc.css docs/

  cd "$srcdir/$_name"
  sed -i 's/COMMAND lua /COMMAND lua5.3 /' awesomeConfig.cmake
  sed -i 's| lua | lua5.3 |;s/lua)/lua5.3)/' tests/examples/CMakeLists.txt
  sed -i '1s|/usr/bin/env lua$|/usr/bin/env lua5.3|' build-utils/check_for_invalid_requires.lua
  sed -i 's/"lua"/"lua5.3"/' tests/test-spawn.lua
  sed -E -i 's/io.stderr:write.*message.*/awesome.emit_signal("debug::error_msg", message)/g' lib/gears/debug.lua
  cd "$srcdir/dotfiles"
  echo "return '$(git rev-parse HEAD)'" > "$srcdir/dotfiles/"release.lua

}

build() {
  cd "$srcdir/$_name/"build
  cmake .. \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DLUA_LIBRARY=/usr/lib/liblua5.3.so \
    -DLUA_INCLUDE_DIR=/usr/include/lua5.3
  make
}

package() {
  cd "$srcdir/$_name/"build
  make -C .  DESTDIR="${pkgdir}" install
  mv "${pkgdir}/usr/share/doc/awesome" "${pkgdir}/usr/share/doc/tde"

  file="$srcdir/$_name/awesome.desktop"
  file2="$srcdir/$_name/tde.desktop"
  # alter the desktop file
  sed -i 's/Name=.*/Name=TDE/g' "$file"
  sed -i 's/^Exec=.*/Exec=env XDG_CURRENT_DESKTOP=TDE awesome/g' "$file"
  mv "$file" "$file2"
  install -Dm 644 "$file2" -t "${pkgdir}/usr/share/xsessions/"
  
  cp -r "$srcdir"/dotfiles/awesome "${pkgdir}/etc/xdg/"
  chmod 666 "${pkgdir}/etc/xdg/awesome/configuration/compton.conf"
  mkdir -p "$pkgdir"/usr/share/backgrounds/
  git clone https://github.com/ODEX-TOS/Pictures "$pkgdir"/usr/share/backgrounds/tos
  # remove the git directory from the final build
  rm -rf "$pkgdir"/usr/share/backgrounds/tos/{.git,.gitignore,.github}
  # remove the readme, only show the png/jpg files
  rm "$pkgdir"/usr/share/backgrounds/tos/README.md
  chmod -R 755 "$pkgdir"/usr/share/backgrounds/tos
  rm "${pkgdir}/usr/share/xsessions/awesome.desktop"
}
